plugins {
    id 'java-library'
    id 'io.qameta.allure' version '2.11.2'
    id 'io.freefair.lombok' version '8.6'
}

def restAssuredVersion = "5.4.0",
        junit5Version = "5.10.2",
        allureVersion = "2.25.0",
        selenideVersion = "6.13.0",
        appiumVersion = "8.3.0",
        ownerVersion = "1.0.12",
        commonsVersion = "2.11.0",
        assertjVersion = "3.25.3",
        apacheCommonsVersion = "1.10.0",
        fakerVersion = "1.0.2"

allure {
    report {
        version.set(allureVersion)
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation(
            "com.codeborne:selenide:$selenideVersion",
            "org.junit.jupiter:junit-jupiter:$junit5Version",
            "io.rest-assured:rest-assured:$restAssuredVersion",
            "io.appium:java-client:$appiumVersion",
            "commons-io:commons-io:$commonsVersion",
            "org.aeonbits.owner:owner:$ownerVersion",
            "io.rest-assured:json-schema-validator:$restAssuredVersion",
            "io.qameta.allure:allure-rest-assured:$allureVersion",
            "io.qameta.allure:allure-selenide:$allureVersion",
            "org.assertj:assertj-core:$assertjVersion",
            "org.apache.commons:commons-text:$apacheCommonsVersion",
            "com.github.javafaker:javafaker:$fakerVersion",)
}

compileJava {
    options.encoding = 'UTF-8'
}
compileTestJava {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    useJUnitPlatform()
    systemProperties(System.getProperties())
    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

task web_test(type: Test) {
    useJUnitPlatform {
        includeTags("auth-server", "auth-user", "cart", "main-page", "search-engine")
    }
}

task mobile_test(type: Test) {
    useJUnitPlatform {
        includeTags("onboarding-page", "main-page-app", "calories-module", "hormonal-module")
    }
}